**Chapter 02 Building blocks**

- Using the interactive shell
- Working with variables
- Organizing your code
- Understanding the type system
- Working with operators
- Understanding the runtime


<!-- vim-markdown-toc GFM -->

* [The interactive shell](#the-interactive-shell)
* [Working  with variables](#working--with-variables)
* [Organizing your code](#organizing-your-code)
    * [Modules](#modules)
    * [Functions](#functions)
    * [Function arity](#function-arity)
    * [Function visibility](#function-visibility)
    * [Imports and aliases](#imports-and-aliases)
    * [Module attributes](#module-attributes)
    * [Comments](#comments)
* [Understanding the type system](#understanding-the-type-system)
    * [Numbers](#numbers)
    * [Atoms](#atoms)
    * [Tuples](#tuples)
    * [Lists](#lists)
    * [Immutability](#immutability)
    * [Maps](#maps)
    * [Binaries and Bitstrings](#binaries-and-bitstrings)
    * [Strings](#strings)
    * [First-class functions](#first-class-functions)
    * [Other built-in types](#other-built-in-types)
    * [Higer-level types](#higer-level-types)
    * [IO lists](#io-lists)
    * [Operators](#operators)
* [Macros](#macros)
* [Understanding the runtime](#understanding-the-runtime)
    * [Modules and functions in the runtime](#modules-and-functions-in-the-runtime)
    * [Starting the runtime](#starting-the-runtime)
* [Summary](#summary)

<!-- vim-markdown-toc -->

# The interactive shell
```
~> iex
Erlang/OTP 21 [erts-10.3.1] [source] [64-bit] [smp:4:4] [ds:4:4:10] [async-threads:1] [hipe]

Interactive Elixir (1.8.1) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)> 
```
```
iex(1)> 1 + 2
3
```
Basic help can be obtained with the h command:
```
iex(4)> h
```
You can also look for the documentation of the IEx module, which is responsible for the shell’s workings:
```
iex(5)> h IEx
```
You can find the same help in the online documentation at https://hexdocs.pm/iex.

# Working  with variables
In Elixir terms, assignment is called *binding*. When you initialize a variable with a value, the variable is bound to that value:
```
iex(6)> monthly_salary = 10000
10000
iex(7)> monthly_salary
10000
iex(8)> monthly_salary * 12
120000
```
Variables can be rebound to a different value:
```
iex(9)> monthly_salary = 11000
11000
iex(10)> monthly_salary
11000
```

# Organizing your code
## Modules
```
iex(1)> IO.puts("Hello World!")
Hello World!
:ok
```

To define your own module `geometry.ex`:
```elixir
defmodule Geometry do
  def rectangle_area(a, b) do
    a * b
  end
end
```
```
~> iex geometry.ex
iex(1)> Geometry.rectangle_area(6, 7)
42
```
```
defmodule Geometry do
    defmodule Rectangle do
    ...
    end
    ...
end
```

## Functions
```elixir
defmodule Geometry do
    def rectangle_area(a, b) do
        a * b
    end
end
```
```elixir
defmodule Geometry do
    def rectangle_area(a, b), do: a * b
end
```

Run as:
```
Geometry.rectangle_area(3, 2)
```
or
```
Geometry.rectangle_area 3, 2
```

Given that Elixir is a functional language, you’ll often need to combine functions, passing the result of one function as the argument to the next one. Elixir comes with a built-in operator, `|>` , called the *pipeline operator*, that does exactly this:
```
iex(5)> -5 |> abs() |> Integer.to_string() |> IO.puts()
5
```
This code is transformed at compile time into the following:
```
iex(6)> IO.puts(Integer.to_string(abs(-5)))
5
```
More generally, the pipeline operator places the result of the previous call as the first argument of the next call. So the following code,
```
prev(arg1, arg2) |> next(arg3, arg4)
```
is translated at compile time to this:
```
next(prev(arg1, arg2), arg3, arg4)
```

Arguably, the pipeline version is more readable because the sequence of execution is read from left to right.
```
-5
|> abs()
|> Integer.to_string()
|> IO.puts()
```


## Function arity
`arity_demo.ex`
```elixir
defmodule Rectangle do
  def area(a), do: area(a, a)
  def area(a, b), do: a * b
end
```

Elixir allows you to specify defaults for arguments by using the `\\` operator followed by the argument’s default value:
```elixir
defmodule Calculator do
    def sum(a, b \\ 0) do
        a + b
    end
end
```

## Function visibility
You can use the `defp` macro to make the function private. A private function can be
used only inside the module it’s defined in, such as `private_fun.ex`
```elixir
defmodule TestPrivate do
  def double(a) do
    sum(a, a)
  end

  defp sum(a, b) do
    a + b
  end
end
```

## Imports and aliases
If your module often calls functions from another module, you can import that other module into your own. The standard library’s Kernel module is automatically imported into every module. `Kernel` contains functions that are often used, so automatic importing makes their use easier.
> You can see what functions are available in the Kernel module by looking in the online documentation at https://hexdocs.pm/elixir/Kernel.html.
```elixir
defmodule MyModule do
    import IO

    def my_function do
        puts "Calling imported function."
    end
end
```



```elixir
defmodule MyModule do
    alias IO, as: MyIO

    def my_function do
        MyIO.puts("Calling imported function.")
    end
end
```

## Module attributes
```elixir
defmodule Circle do
    @pi 3.14159

    def area(r), do: r*r*@pi
    def circumference(r), do: 2*r*@pi
end
```

Elixir registers some module attributes by default. For example, the attributes `@moduledoc` and `@doc` can be used to provide documentation for modules and functions:

`circle.ex`

```elixir
defmodule Circle do
    @moduledoc "Implements basic circle functions"
    @pi 3.14159

    @doc "Computes the area of a circle"
    def area(r), do: r*r*@pi

    @doc "Computes the circumference of a circle"
    def circumference(r), do: 2*r*@pi
end
```
Run `elixirc circle.ex` . This will generate the file Elixir.Circle.beam.
```
ex(2)> h Circle

                                     Circle                                     

Implements basic circle functions

iex(3)> h Circle.area

                                  def area(r)                                   

Computes the area of a circle
```

**Type specifications**

Type specifications (often called *typespecs*) are another important feature based on attributes. This feature allows you to provide type information for your functions, which can later be analyzed with a static analysis tool called `dialyzer` (http://erlang.org/doc/man/dialyzer.html).
```elixir
defmodule Circle do
    @pi 3.14159

    @spec area(number) :: number
    def area(r), do: r*r*@pi

    @spec circumference(number) :: number
    def circumference(r), do: 2*r*@pi
end
```
You can find a detailed reference in the official docs at https://hexdocs.pm/elixir/typespecs.html.

## Comments
Comments in Elixir start with the character `#` .
```elixir
# This is a comment
a = 3.14 # so is this
```

# Understanding the type system
## Numbers
```
iex(1)> 3
3 
# Integer
iex(2)> 0xFF
255 
# Integer written in hex
iex(3)> 3.14
3.14 
# Float
iex(4)> 1.0e-2
0.01
# Float, exponential notation
```

```
iex(5)> 1 + 2 * 3
7
iex(6)> 4/2
2.0
iex(7)> 3/2
1.5
iex(8)> div(5,2)
2
iex(9)> rem(5,2)
1
iex(10)> 1_000_000
1000000
iex(11)> 999999999999999999999999999999999999999999999999999999999999
999999999999999999999999999999999999999999999999999999999999
```

If you’re worried about memory size, it’s best to consult the official Erlang memory guide at http://erlang.org/doc/efficiency_guide/advanced.html.

## Atoms
```
:an_atom
:another_atom
:"an atom with spaces"
```
**Aliases**
```
iex(1)> AnAtom == :"Elixir.AnAtom"
true
iex(2)> AnAtom == Elixir.AnAtom
true
iex(3)> alias IO, as: MyIO
iex(4)> MyIO.puts("Hello!")
Hello!
iex(5)> MyIO == Elixir.IO
true
```

**Atoms as Booleans**
```
iex(1)> :true == true
true
iex(2)> :false == false
true
iex(3)> true and false
false
iex(4)> false or true
true
iex(5)> not false
true
iex(6)> not :an_atom_other_than_true_or_false
** (ArgumentError) argument error
```

**Nil and Truthy values**
```
iex(1)> nil == :nil
true
iex(2)> nil || false || 5 || true 
5
```

```
iex(1)> true && 5
5
iex(2)> false && 5
false
iex(3)> nil && 5
nil
```

## Tuples
```
iex(1)> person = {"Bob", 25}
{"Bob", 25}
iex(2)> age = elem(person, 1)
25
iex(3)> put_elem(person, 1, 26)
{"Bob", 26}
iex(4)> person
{"Bob", 25}
iex(5)> older_person = put_elem(person, 1, 26)
{"Bob", 26}
iex(6)> older_person
{"Bob", 26}
iex(7)> person = put_elem(person, 1, 26)
{"Bob", 26}
```

## Lists
```
iex(1)> prime_numbers = [2, 3, 5, 7]
[2, 3, 5, 7]
iex(2)> length(prime_numbers)
4
iex(3)> Enum.at(prime_numbers, 3)
7
iex(4)> 5 in prime_numbers
true
iex(5)> 4 in prime_numbers
false
iex(6)> List.replace_at(prime_numbers, 0, 11)
[11, 3, 5, 7]
iex(7)> new_primes = List.replace_at(prime_numbers, 0, 11)
[11, 3, 5, 7]
iex(8)> prime_numbers = List.replace_at(prime_numbers, 0, 11)
[11, 3, 5, 7]
```
```
iex(9)> List.insert_at(prime_numbers, 3, 13)
[11, 3, 5, 13, 7]
iex(10)> List.insert_at(prime_numbers, -1, 13)
[11, 3, 5, 7, 13]
iex(11)> [1, 2, 3] ++ [4, 5]
[1, 2, 3, 4, 5]
```

**Recursive List Definition**
```
a_list = [head | tail]
```
```
iex(1)> hd([1, 2, 3, 4])
1
iex(2)> tl([1, 2, 3, 4])
[2, 3, 4]
```
```
iex(1)> a_list = [5, :value, true]
[5, :value, true]
iex(2)> new_list = [:new_element | a_list]
[:new_element, 5, :value, true]
```

## Immutability
**Modifying Tuples**
```
a_tuple = {a, b, c}
new_tuple = put_elem(a_tuple, 1, b2)
```

**Modifying Lists**

**Benefits**
Immutability may seem strange, and you may wonder about its purpose. There are two important benefits of immutability:
- Side-effect-free functions
- Data consistency

## Maps
```
iex(1)> empty_map = %{}
```
```
iex(2)> squares = %{1 => 1, 2 => 4, 3 => 9}
iex(3)> squares = Map.new([{1, 1}, {2, 4}, {3, 9}])
%{1 => 1, 2 => 4, 3 => 9}
```
```
iex(4)> squares[2]
4
iex(5)> squares[4]
nil
```
```
iex(6)> Map.get(squares, 2)
4
iex(7)> Map.get(squares, 4)
nil
iex(8)> Map.get(squares, 4, :not_found)
:not_found
```
```
iex(9)> Map.fetch(squares, 2)
{:ok, 4}
iex(10)> Map.fetch(squares, 4)
:error
```
```
iex(11)> Map.fetch!(squares, 2)
4
iex(12)> Map.fetch!(squares, 4)
** (KeyError) key 4 not found in: %{1 => 1, 2 => 4, 3 => 9}
(stdlib) :maps.get(4, %{1 => 1, 2 => 4, 3 => 9})
To store a new element to the map, you can use Map.put/3 :
iex(13)> squares = Map.put(squares, 4, 16)
%{1 => 1, 2 => 4, 3 => 9, 4 => 16}
iex(14)> squares[4]
16
```

**Structured Data**
```
iex(1)> bob = %{:name => "Bob", :age => 25, :works_at => "Initech"}
iex(2)> bob = %{name: "Bob", age: 25, works_at: "Initech"}
```
```
iex(3)> bob[:works_at]
"Initech"
iex(4)> bob[:non_existent_field]
nil
iex(5)> bob.age
25
```
```
iex(7)> next_years_bob = %{bob | age: 26}
%{age: 26, name: "Bob", works_at: "Initech"}
iex(8)> %{bob | age: 26, works_at: "Initrode"}
%{age: 26, name: "Bob", works_at: "Initrode"}
iex(9)> %{bob | works_in: "Initech"}
** (KeyError) key :works_in not found
```

## Binaries and Bitstrings
```
iex(1)> <<1, 2, 3>>
<<1, 2, 3>>
```
```
iex(2)> <<256>>
<<0>>
iex(3)> <<257>>
<<1>>
iex(4)> <<512>>
<<0>>
```
```
iex(5)> <<257::16>>
<<1, 1>>
iex(6)> <<1::4, 15::4>>
<<31>>
iex(7)> <<1::1, 0::1, 1::1>>
<<5::size(3)>>
iex(8)> <<1, 2>> <> <<3, 4>>
<<1, 2, 3, 4>>
```

## Strings
```
iex(1)> "This is a string"
"This is a string"
```
```
iex(2)> "Embedded expression: #{3 + 0.14}"
"Embedded expression: 3.14"
```
```
iex(3)> "\r \n \" \\"
iex(5)> ~s(This is also a string)
"This is also a string"
iex(6)> ~s("Do... or do not. There is no try." -Master Yoda)
"\"Do... or do not. There is no try.\" -Master Yoda"
```
```
iex(7)> ~S(Not interpolated #{3 + 0.14})
"Not interpolated \#{3 + 0.14}"
iex(8)> ~S(Not escaped \n)
"Not escaped \\n"
iex(9)> """
Heredoc must end on its own line """
"""
"Heredoc must end on its own line \"\"\"\n"
iex(10)> "String" <> " " <> "concatenation"
"String concatenation"
```

**Character Lists**
```
iex(1)> 'ABC'
'ABC'
iex(2)> [65, 66, 67]
'ABC'
iex(3)> 'Interpolation: #{3 + 0.14}'
'Interpolation: 3.14'
iex(4)> ~c(Character list sigil)
'Character list sigil'
iex(5)> ~C(Unescaped sigil #{3 + 0.14})
'Unescaped sigil \#{3 + 0.14}'
iex(6)> '''
Heredoc
'''
'Heredoc\n'
iex(7)> String.to_charlist("ABC")
'ABC'
```

## First-class functions
In Elixir, a function is a first-class citizen, which means it can be assigned to a variable.
```
iex(1)> square = fn x ->
    x * x
end
```
```
iex(2)> square.(5)
25
```
```
iex(3)> print_element = fn x -> IO.puts(x) end
#Function<6.128620087/1 in :erl_eval.expr/5>
iex(4)> Enum.each([1, 2, 3], print_element)
1
2
3
:ok
```
```
iex(5)> Enum.each(
[1, 2, 3],
fn x -> IO.puts(x) end
)
1
2
3
:ok
```
```
iex(6)> Enum.each([1,2,3], &IO.puts/1)
1
2
3
:ok
```
The capture operator can also be used to shorten the lambda definition, making it
possible to omit explicit argument naming. For example, you can turn this definition
```
iex(7)> lambda = fn x, y, z -> x * y + z end
```
into a more compact form:
```
iex(8)> lambda = &(&1 * &2 + &3)
```
```
iex(9)> lambda.(2, 3, 4)
10
```


**Closures**
```
iex(1)> outside_var = 5
5
iex(2)> my_lambda = fn ->
            IO.puts(outside_var)
        end
iex(3)> my_lambda.()
5
```
```
iex(1)> outside_var = 5
iex(2)> lambda = fn -> IO.puts(outside_var) end
iex(3)> outside_var = 6
iex(4)> lambda.()
5
```

## Other built-in types
- *reference*
- *pid*
- *port identifier*

## Higer-level types
**Range**
```
iex(1)> range = 1..2
iex(2)> 2 in range
true
iex(3)> -1 in range
false
```
```
iex(4)> Enum.each(
1..3,
&IO.puts/1
)
1
2
3
```

**Keyword Lists**
```
iex(1)> days = [{:monday, 1}, {:tuesday, 2}, {:wednesday, 3}]
iex(2)> days = [monday: 1, tuesday: 2, wednesday: 3]
```
```
iex(3)> Keyword.get(days, :monday)
1
iex(4)> Keyword.get(days, :noday)
nil
iex(5)> days[:tuesday]
2
```
```
iex(6)> IO.inspect([100, 200, 300])
[100, 200, 300]
iex(7)> IO.inspect([100, 200, 300], [width: 3])
[100,
200,
300]
iex(8)> IO.inspect([100, 200, 300], width: 3, limit: 1)
[100,
...]
```

**MapSet**
A `MapSet` is the implementation of a set — a store of unique values, where a value can be of any type.
```
iex(1)> days = MapSet.new([:monday, :tuesday, :wednesday])
#MapSet<[:monday, :tuesday, :wednesday]>
iex(2)> MapSet.member?(days, :monday)
true
iex(3)> MapSet.member?(days, :noday)
false
iex(4)> days = MapSet.put(days, :thursday)
#MapSet<[:monday, :thursday, :tuesday, :wednesday]>
iex(5)> Enum.each(days, &IO.puts/1)
monday
thursday
tuesday
wednesday
```

**Times and Dates**

Elixir has a couple of modules for working with date and time types:
- `Date`
- `Time`
- `DateTime`
- `NaiveDateTime`


A date can be created with the `~D` sigil. The following example creates a date that
represents January 31, 2018:
```
iex(1)> date = ~D[2018-01-31]
~D[2018-01-31]
iex(2)> date.year
2018
iex(3)> date.month
1
```
Similarly, you can represent a time with the `~T` sigil, by providing hours, minutes, seconds, and microseconds:
```
iex(1)> time = ~T[11:59:12.00007]
iex(2)> time.hour
11
iex(3)> time.minute
59
```

In addition to these two types, you can also work with datetimes using the `NaiveDateTime` and `DateTime` modules. The naive version can be created with the `~N` sigil:
```
iex(1)> naive_datetime = ~N[2018-01-31 11:59:12.000007]
iex(2)> naive_datetime.year
2018
iex(3)> naive_datetime.hour
11
```

The `DateTime` module can be used to work with datetimes in some timezone. Unlike
with other types, no sigil is available. Instead, you can create a datetime by using `DateTime` functions:
```
iex(4)> datetime = DateTime.from_naive!(naive_datetime, "Etc/UTC")
iex(5)> datetime.year
2018
iex(6)> datetime.hour
11
iex(7)> datetime.time_zone
"Etc/UTC"
```

## IO lists
An IO list is a special sort of list that’s useful for incrementally building output that will be forwarded to an I/O device, such as a network or a file. Each element of an IO list must be one of the following:
- An integer in the range of 0 to 255
- A binary
- An IO list

```
iex(1)> iolist = [[['H', 'e'], "llo,"], " worl", "d!"]
iex(2)> IO.puts(iolist)
Hello, world!
```

## Operators
**Table 2.1  Comparison operators**

Operator|Description
----|----
=== , !== |Strict equality/inequality
==, != |Weak equality/inequality
< , > , ≤ , ≥ |Less than, greater than, less than or equal, greater than or equal

```
iex(1)> 1 == 1.0
true
iex(2)> 1 === 1.0
false
```

# Macros
Macros are arguably one of the most important features Elixir brings to the table, compared to plain Erlang. They make it possible to perform powerful code transformations in compile time, thus reducing boilerplate and providing elegant, mini-DSL constructs.

# Understanding the runtime
## Modules and functions in the runtime
## Starting the runtime

# Summary
- Elixir code is divided into modules and functions.
- Elixir is a dynamic language. The type of a variable is determined by the value it holds.
- Data is immutable — it can’t be modified. A function can return the modified
- version of the input that resides in another memory location. The modified version shares as much memory as possible with the original data.
- The most important primitive data types are numbers, atoms, and binaries.
- There is no Boolean type. Instead, the atoms true and false are used.
- There is no nullability. The atom nil can be used for this purpose.
- There is no string type. Instead, you can use either binaries (recommended) or lists (when needed).
- The built-in complex types are tuples, lists, and maps. Tuples are used to group a small, fixed-size number of fields. Lists are used to manage variable-size collections. A map is a key/value data structure.
- Range , keyword lists, MapSet , Date , Time , NaiveDateTime , and DateTime are abstractions built on top of the existing built-in types.
- Functions are first-class citizens.
- Module names are atoms (or aliases) that correspond to .beam files on the disk.
- There are multiple ways of starting programs: iex , elixir , and the mix tool.
